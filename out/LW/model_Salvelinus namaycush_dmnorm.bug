
    model {               
    
    #### Process model -- effects of taxonomy
    
    # given the likelihood distributions and the priors, 
    # create normal posterior distributions for log10a, b, 
    # and for the process error (=between species variability sigmaGS)  
    
    abTrue[1] ~ dnorm(prior_mean_log10a,prior_tau_log10a)   # given the data and the prior, get normal posterior distribution for log10a
    abTrue[2] ~ dnorm(prior_mean_b,prior_tau_b)             # given the data and the prior, get normal posterior distribution for b
    sigmaGSlog10a ~ dgamma( SD_rGS_log10a, SD_muGS_log10a)  # given the data and the prior, establish posterior distribution for measurement error in log10a  
    sigmaGSb ~ dgamma( SD_rGS_b, SD_muGS_b)                 # given the data and the prior, establish posterior distribution for measurement error in b
    
    # given the posterior distributions and the process errors,
    # establish for every species the expected witin-species 
    # parameter distributions; no correlation roGS between species 
    
    roGS <- 0 
    tauGenusSpecies[1] <- pow(sigmaGSlog10a,-2)
    tauGenusSpecies[2] <- pow(sigmaGSb,-2)
    for(k in 1:Nspecies){
    abGenusSpecies[k,1] ~ dnorm(abTrue[1],tauGenusSpecies[1]) 
    abGenusSpecies[k,2] ~ dnorm(abTrue[2],tauGenusSpecies[2]) 
    }
    
    
    ### Observation model  
    
    ## Errors 
    
    # given the data and the priors, establish distributions  	
    # for the observation errors sigmaObs 	
    
    sigmaObslog10a ~ dgamma( SD_rObs_log10a, SD_muObs_log10a) 
    sigmaObsb ~ dgamma( SD_rObs_b, SD_muObs_b) 	
    
    # create inverse covariance matrix, with negative parameter correlation roObs
    roObs ~ dunif(-0.99,0)  # uniform prior for negative correlation between log10a and b
    CovObs[1,1] <- pow(sigmaObslog10a,2)  
    CovObs[2,2] <- pow(sigmaObsb,2) 
    CovObs[1,2] <- roObs * sigmaObslog10a * sigmaObsb 
    CovObs[2,1] <- CovObs[1,2]
    TauObs[1:2,1:2] <- inverse(CovObs[1:2,1:2]) 
    
    ## likelihood
    
    # given the data, the priors and the covariance, 
    # create multivariate likelihood distributions for log10(a) and b 
    
    for(i in 1:N){
    TauObsI[i,1:2,1:2] <- TauObs[1:2,1:2] * pow(Weights[i],2)   # weighted precision
    ab[i,1:2] ~ dmnorm(abGenusSpecies[GenusSpecies[i],1:2],TauObsI[i,1:2,1:2])   
    }
    }
    